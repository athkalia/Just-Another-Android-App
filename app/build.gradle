buildscript {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$versions.androidPlugin"
        classpath "me.tatarka:gradle-retrolambda:$versions.retrolambdaPlugin"
        classpath "com.jakewharton:butterknife-gradle-plugin:$versions.butterknife"
        classpath "com.github.triplet.gradle:play-publisher:$versions.playPublisherPlugin"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:$versions.dexCountPlugin"
        classpath "io.fabric.tools:gradle:$versions.fabricPlugin"
        classpath "com.vanniktech:gradle-android-apk-size-plugin:$versions.apkSizePlugin"
        classpath "com.airbnb.okreplay:gradle-plugin:$versions.okReplayFakeServerResponses"
    }

}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.github.triplet.play' // Plugin to publish apk to play store.
apply plugin: 'com.jakewharton.butterknife'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.vanniktech.android.apk.size'
apply plugin: 'okreplay' // See https://github.com/airbnb/okreplay

apply from: '../config/gradle/static_analysis_java.gradle'
apply from: 'config/gradle/lint.gradle'
apply from: 'config/gradle/jacoco.gradle'
apply from: 'config/gradle/grant_animation_permission.gradle'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 25
    buildToolsVersion versions.buildTools

    defaultConfig {
        // Speed hack to decrease development environment build times. See gradle.properties file for more details about enabling this.
        minSdkVersion increaseMinSdkForDev.toBoolean() ? 21 : 16
        targetSdkVersion 25
        applicationId 'com.justanotherandroidapp'
        versionName version // Comes from gradle.properties
        versionCode buildVersionCode() // Comes from versioning.gradle
        vectorDrawables.useSupportLibrary true
        testApplicationId 'com.justanotherandroidapp.test'
        testInstrumentationRunner 'com.example.util.runner.EspressoTestRunner'

        // Fetch fabric api key from relevant properties file and place it in android manifest.
        def keystorePropertiesFile = file("src/main/assets/project.properties")
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
        manifestPlaceholders = [fabric_api_key: keystoreProperties['fabricApiKey']]

        // Speed up the build by fetching min. number of resources required for dev builds, and no extra languages overall than the ones
        // supported in the project. See https://developer.android.com/studio/build/shrink-code.html for more details.
        if (decreaseAmountOfResourcesForDev.toBoolean()) { resConfigs "en" } else { resConfigs("en", "hdpi") }
    }

    // Speed up the build by disabling the cruncher that optimizes pngs
    if (disableAaptCruncherForDev.toBoolean()) { aaptOptions.cruncherEnabled = false }

    signingConfigs {
        release {
            // Fetch signing config from relevant properties file
            def keystorePropertiesFile = file("config/signing/signing.properties")
            def keystoreProperties = new Properties()
            keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storePassword keystoreProperties['storePassword']
            storeFile file('config/signing/release.keystore')
        }

        buildTypes {
            debug {
                resValue 'string', 'app_name', 'Example.debug'
                applicationIdSuffix '.develop'
                // These 2 lines allow incremental builds to work properly
                buildConfigField "String", "GIT_SHA", "\"DEBUG\""
                buildConfigField "String", "BUILD_TIME", "\"DEBUG\""
                debuggable true
                minifyEnabled false
                testCoverageEnabled true // Enable jacoco for espresso tests. For espresso and mixed see jacoco.gradle file.
                multiDexEnabled true
            }

            qa.initWith debug
            qa {
                resValue 'string', 'app_name', 'Example.qa'
                applicationIdSuffix ".qa"
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }

            release {
                resValue 'string', 'app_name', 'Example'
                def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
                def buildTime = new GregorianCalendar().format("MM-dd-yyyy' 'h:mm:ss a z")
                // Gather build information for release builds and propagate to fabric.
                buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
                buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
                minifyEnabled true
                signingConfig signingConfigs.release
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }

    }

    variantFilter { variant ->
        def buildType = variant.buildType.name;
        // Speed hack to decrease dev build times. See gradle.properties file for more details about enabling this.
        if (disableSomeBuildTypes.toBoolean() && !buildType.equals(disableAllBuildTypesApartFrom)) {
            variant.setIgnore(true);
        }
    }

    sourceSets {
        def CODE_SHARED_BY_DEBUG_AND_QA_BUILD_TYPES = 'src/debugAndQa/java'
        def RES_SHARED_BY_DEBUG_AND_QA_BUILD_TYPES = 'src/debugAndQa/res'
        def UNIT_TESTS_LOCATION = 'src/unitTests/java'
        def INTEGRATION_TESTS_LOCATION = 'src/integrationTests/java'
        def HELPERS_SHARED_ACROSS_ALL_TESTS = 'src/testHelpers/java'
        def RESOURCES_SHARED_ACROSS_ALL_TESTS = 'src/testHelpers/resources'
        def DEBUG_ONLY_BUILD_TYPE_UNIT_TESTS = 'src/unitTestsDebug/java'
        def QA_ONLY_BUILD_TYPE_UNIT_TESTS = 'src/unitTestsQa/java'
        def RELEASE_ONLY_BUILD_TYPE_UNIT_TESTS = 'src/unitTestsRelease/java'
        def ESPRESSO_TESTS_LOCATION = 'src/espressoTests/java'
        def ESPRESSO_INTEGRATION_TESTS_LOCATION = 'src/espressoIntegrationTests/java'
        def ESPRESSO_ASSETS_LOCATION_1 = 'src/espressoTests/assets'
        def ESPRESSO_ASSETS_LOCATION_2 = 'src/espressoIntegrationTests/assets'

        // Code related source sets
        debug.java.srcDirs += [CODE_SHARED_BY_DEBUG_AND_QA_BUILD_TYPES]
        qa.java.srcDirs += [CODE_SHARED_BY_DEBUG_AND_QA_BUILD_TYPES]
        debug.res.srcDirs += [RES_SHARED_BY_DEBUG_AND_QA_BUILD_TYPES]
        qa.res.srcDirs += [RES_SHARED_BY_DEBUG_AND_QA_BUILD_TYPES]

        // Test related source sets
        test.java.srcDirs += [UNIT_TESTS_LOCATION, INTEGRATION_TESTS_LOCATION, HELPERS_SHARED_ACROSS_ALL_TESTS]
        test.resources.srcDirs += [RESOURCES_SHARED_ACROSS_ALL_TESTS]
        testDebug.java.srcDirs += [DEBUG_ONLY_BUILD_TYPE_UNIT_TESTS]
        testQa.java.srcDirs += [QA_ONLY_BUILD_TYPE_UNIT_TESTS]
        testRelease.java.srcDirs += [RELEASE_ONLY_BUILD_TYPE_UNIT_TESTS]

        // Espresso test related source sets
        androidTest.java.srcDirs += [ESPRESSO_TESTS_LOCATION, ESPRESSO_INTEGRATION_TESTS_LOCATION, HELPERS_SHARED_ACROSS_ALL_TESTS]
        androidTest.resources.srcDirs += [RESOURCES_SHARED_ACROSS_ALL_TESTS]
        androidTest.assets.srcDirs += [ESPRESSO_ASSETS_LOCATION_1, ESPRESSO_ASSETS_LOCATION_2]
    }

    testOptions {
        unitTests.all {
            systemProperty 'robolectric.logging.enabled', 'true'
            systemProperty 'robolectric.logging', 'stdout'
            // Print test details in the terminal (or CI).
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
                exceptionFormat 'full'
            }
        }
    }

    compileOptions {
        encoding 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        preDexLibraries isRunningInCI.toBoolean()
        javaMaxHeapSize dexMemoryAllocation
        dexInProcess true
    }

    // Grant all runtime permissions when installing an apk from the command line or android studio. For now this is only required for
    // the OkReplay library (https://github.com/airbnb/okreplay) as it requires the EXTERNAL STORAGE permission to record the server
    // responses.
    if (grantAllRuntimePermissions.toBoolean()) { adbOptions.installOptions "-g" }

}

// Plugin to manage automated publishing to the playstore. See https://github.com/Triple-T/gradle-play-publisher
play {
    serviceAccountEmail = '560654334828-compute@developer.gserviceaccount.com'
    pk12File = file(pk12FileLocation) // See gradle.properties for details
    track = releaseTrack // See gradle.properties for details
    uploadImages = true
    userFraction = 0.05 // Only necessary for 'rollout'. This means that a rollout for 5% of the users is performed.
    untrackOld = true // will remove the 'alpha' release when a new higher version code 'beta' is uploaded.
}

// Settings for the retrolambda plugin. See https://github.com/evant/gradle-retrolambda
retrolambda {
    javaVersion JavaVersion.VERSION_1_7
    incremental true
}

// Settings for the dexcount gradle plugin. See https://github.com/KeepSafe/dexcount-gradle-plugin
// When running the plugin results appear both in the command line as well as in the "build/outputs/dexcount/" folder
dexcount {
    format = "list"
    includeClasses = false
    includeFieldCount = true
    includeTotalMethodCount = true
    orderByMethodCount = false
    verbose = true
    maxTreeDepth = Integer.MAX_VALUE
    enableForInstantRun = false
    runOnEachAssemble = false
}

// Fail the build when the release APK is more than the number defined in the gradle.properties file via 'maxReleaseApkSizeInBytes' property.
gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(":app:assembleRelease")) {
        apkSize.maxApkSize = maxReleaseApkSizeInBytes.toInteger()
    }
}

dependencies {
    // MODULE DEPENDENCIES *****************************************************************************************************************
    // Import the common module into this module
    compile project(':common')

    // MAIN DEPENDENCIES *******************************************************************************************************************
    compile projectDependencies.supportLibsAppCompat
    compile projectDependencies.supportLibsAnnotations
    compile projectDependencies.supportLibsRecyclerview
    compile projectDependencies.supportLibsConstraintLayout
    compile projectDependencies.supportLibsMultiDex

    compile projectDependencies.crashlytics
    compile projectDependencies.answersAnalytics

    annotationProcessor projectDependencies.daggerAnnotationProcessor
    compile projectDependencies.dagger
    compile projectDependencies.daggerAndroid
    annotationProcessor projectDependencies.daggerAndroidAnnotationProcessor

    compile projectDependencies.timber

    compile projectDependencies.mosbyMvp
    compile projectDependencies.mosbyViewState
    compile projectDependencies.mosbyNullPresenter

    compile projectDependencies.nullabilityAnnotations
    androidTestCompile projectDependencies.nullabilityAnnotations //fixes version conflict

    compile projectDependencies.butterknife
    annotationProcessor projectDependencies.butterknifeAnnotationProcessor

    compile projectDependencies.retrofit
    compile projectDependencies.retrofitGson
    compile projectDependencies.okhttpLoggingInterceptor
    compile projectDependencies.retrofitRxJava2Adapter

    compile projectDependencies.rxAndroid
    compile projectDependencies.rxJava2

    compile projectDependencies.glideImageLoading

    provided projectDependencies.autoValue
    annotationProcessor projectDependencies.autoValue

    // Synchronizing app with espresso. We could have added this only for debug/qa build types and hidden it behind a no-op interface (like
    // Stetho), but it's tiny. No point.
    compile projectDependencies.espressoIdlingResource

    compile projectDependencies.jodaTimeDanLew

    compile projectDependencies.appShortcutsLibrary
    annotationProcessor projectDependencies.appShortcutsLibraryAnnotationProcessor

    compile projectDependencies.lottieAnimations

    // This is 'provided' as it's not really needed at runtime. The generated code is the only thing needed (and this happens to have a
    // dependency on guava).
    provided projectDependencies.autoFactory
    annotationProcessor projectDependencies.autoFactory

    compile projectDependencies.permissionsDispatcher
    annotationProcessor projectDependencies.permissionsDispatcherAnnotationProcessor

    // DEBUG DEPENDENCIES ******************************************************************************************************************
    debugCompile(projectDependencies.stetho) {
        exclude module: 'jsr305'
    }
    debugCompile(projectDependencies.stethoOkHttp) {
        exclude module: 'jsr305'
    }
    qaCompile(projectDependencies.stetho) {
        exclude module: 'jsr305'
    }
    qaCompile(projectDependencies.stethoOkHttp) {
        exclude module: 'jsr305'
    }

    debugCompile projectDependencies.rxLint

    debugCompile projectDependencies.retrofitMockMode
    qaCompile projectDependencies.retrofitMockMode

    debugCompile projectDependencies.chuckNetworkInterceptor
    qaCompile projectDependencies.chuckNetworkInterceptor
    releaseCompile projectDependencies.chuckNetworkInterceptorNoOp

    debugCompile projectDependencies.traceurRxJavaLogging
    qaCompile projectDependencies.traceurRxJavaLogging

    debugCompile projectDependencies.sherlockLibrary
    qaCompile projectDependencies.sherlockLibrary
    releaseCompile projectDependencies.sherlockLibraryNoOp

    provided projectDependencies.findbugsAnnotations

    debugCompile projectDependencies.okReplayFakeServerResponses
    qaCompile projectDependencies.okReplayFakeServerResponses
    releaseCompile projectDependencies.okReplayFakeServerResponsesNoOp

    // TEST DEPENDENCIES *******************************************************************************************************************
    testCompile projectDependencies.jUnit

    testCompile projectDependencies.assertJJava8

    testCompile projectDependencies.mockito

    testCompile projectDependencies.robolectric
    testCompile projectDependencies.robolectricProblemFix
    testCompile projectDependencies.robolectricMultiDex

    testCompile projectDependencies.okhttpMockWebServer

    testCompile projectDependencies.jodaTimeDanLew

    testCompile projectDependencies.burstTesting

    // INSTRUMENTATION TESTS DEPENDENCIES **************************************************************************************************
    androidTestCompile projectDependencies.okReplayFakeServerResponsesEspresso

    androidTestCompile projectDependencies.cappucinoAnimationDisabling

    androidTestCompile projectDependencies.espresso
    androidTestCompile projectDependencies.espressoIntents
    androidTestCompile projectDependencies.espressoTestRunner
    androidTestCompile projectDependencies.espressoTestRules
    androidTestCompile(projectDependencies.espressoContrib) {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }

    // Force specific library version for instrumentation tests. Fixes version conflict issue.
    androidTestCompile projectDependencies.supportLibsAnnotations

    // For instrumentation tests the java 7 compiled library is used to avoid dex errors.
    androidTestCompile projectDependencies.assertJJava7

    // Mocking the backend
    androidTestCompile projectDependencies.okhttpMockWebServer

}
