ext.versions = [

        // General
        buildToolsVersion         : '25.0.1',

        // Plugins
        androidPluginVersion      : '2.2.3',
        retrolambdaPluginVersion  : '3.3.1',
        dexCountPluginVersion     : '0.6.1',
        playPublisherPluginVersion: '1.1.5',
        fabricPluginVersion       : '1.22.0',
        jacocoPluginVersion       : '0.7.7.201606060606',

        // Libraries
        supportLibrariesVersion   : '25.1.1',
        crashlyticsVersion        : '2.6.5',
        answersAnalyticsVersion   : '1.3.10',
        butterknifeVersion        : '8.5.1',
        assertJJava8Version       : '3.5.2',
        assertJJava7Version       : '2.5.0',
        jUnitVersion              : '4.12',
        espressoVersion           : '2.2.2',
        espressoTestRulesVersion  : '0.5',
        espressoTestRunnerVersion : '0.5',
        timberVersion             : '4.5.1',
        mosbyMvpVersion           : '2.0.1',
        annotationsVersion        : '2.0.1',
        daggerVersion             : '2.7',
        retrofitVersion           : '2.1.0',
        rxJavaVersion             : '1.2.4',
        rxAndroidVersion          : '1.2.1',
        loggingInterceptorVersion : '3.4.2',
        stethoVersion             : '1.4.2',
        stethoOkHttpVersion       : '1.4.2',
        robolectricVersion        : '3.2.2',
        glideVersion              : '3.7.0',
        autoValueVersion          : '1.3',
        rxLintVersion             : '1.1',
        okhttpMockWebServerVersion: '3.4.2',
        jodaTimeVersion           : '2.9.4.1',
        mockitoVersion            : '2.6.3',
]

ext {
    /**
     * Builds an Android version code from the version of the project.
     * This is designed to handle the -SNAPSHOT and -RC format.
     *
     * I.e. during development the version ends with -SNAPSHOT. As the code stabilizes and release nears
     * one or many Release Candidates are tagged. These all end with "-RC1", "-RC2" etc.
     * And the final release is without any suffix.
     * @return
     */
    buildVersionCode = {
        //The rules is as follows:
        //-SNAPSHOT counts as 0
        //-RC* counts as the RC number, i.e. 1 to 98
        //final release counts as 99.
        //Thus you can only have 98 Release Candidates, which ought to be enough for everyone

        def candidate = "99"
        def (major, minor, patch) = version.toLowerCase().replaceAll('-', '').tokenize('.')
        if (patch.endsWith("snapshot")) {
            candidate = "0"
            patch = patch.replaceAll("[^0-9]", "")
        } else {
            def rc
            (patch, rc) = patch.tokenize("rc")
            if (rc) {
                candidate = rc
            }
        }

        (major, minor, patch, candidate) = [major, minor, patch, candidate].collect { it.toInteger() }

        (major * 1000000) + (minor * 10000) + (patch * 100) + candidate;
    }

}
